<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
"http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
<html xmlns='http://www.w3.org/1999/xhtml'>

<head>
<title>Useful Scripts</title>
<meta name="generator" content="Halibut version 1.0 (NSIS Custom Build) xhtml-backend" />
<link rel="stylesheet" href="style.css" type='text/css' />
</head>

<body>
<p><a href='AppendixB.html'>Previous</a> | <a href='Contents.html'>Contents</a> | <a href='AppendixD.html'>Next</a></p>
<ul>
<li><a class="btitle" href="AppendixC.html#C"><b>Appendix C: </b>Useful Scripts</a></li>
<ul>
<li><a href="AppendixC.html#C.1">Get parent directory</a></li>
<li><a href="AppendixC.html#C.2">Trim newlines</a></li>
<li><a href="AppendixC.html#C.3">Get command line parameters</a></li>
<li><a href="AppendixC.html#C.4">Search in a string</a></li>
<li><a href="AppendixC.html#C.5">Get Windows version</a></li>
<li><a href="AppendixC.html#C.6">Get Internet Explorer version</a></li>
<li><a href="AppendixC.html#C.7">Is .NET Framework installed?</a></li>
<li><a href="AppendixC.html#C.8">Is Macromedia Flash Player installed?</a></li>
<li><a href="AppendixC.html#C.9">Connect to the Internet</a></li>
<li><a href="AppendixC.html#C.10">Get Installer Filename</a></li>
<li><a href="AppendixC.html#C.11">Prevent Multiple Instances</a></li>
<li><a href="AppendixC.html#C.12">More</a></li>
</ul>
</ul>
<a name="C"></a><h1>Appendix C: Useful Scripts</h1>
<a name="C.1"></a><h2>C.1 Get parent directory</h2>
<pre> ; GetParent
 ; input, top of stack  (e.g. C:\Program Files\Poop)
 ; output, top of stack (replaces, with e.g. C:\Program Files)
 ; modifies no other variables.
 ;
 ; Usage:
 ;   Push &quot;C:\Program Files\Directory\Whatever&quot;
 ;   Call GetParent
 ;   Pop $R0
 ;   ; at this point $R0 will equal &quot;C:\Program Files\Directory&quot;

 Function GetParent
 
   Exch $R0
   Push $R1
   Push $R2
   Push $R3
   
   StrCpy $R1 0
   StrLen $R2 $R0
   
   loop:
     IntOp $R1 $R1 + 1
     IntCmp $R1 $R2 get 0 get
     StrCpy $R3 $R0 1 -$R1
     StrCmp $R3 &quot;\&quot; get
     Goto loop
   
   get:
     StrCpy $R0 $R0 -$R1
     
     Pop $R3
     Pop $R2
     Pop $R1
     Exch $R0
     
 FunctionEnd
</pre>
<a name="C.2"></a><h2>C.2 Trim newlines</h2>
<pre> ; TrimNewlines
 ; input, top of stack  (e.g. whatever$\r$\n)
 ; output, top of stack (replaces, with e.g. whatever)
 ; modifies no other variables.

 Function TrimNewlines
   Exch $R0
   Push $R1
   Push $R2
   StrCpy $R1 0
 
 loop:
   IntOp $R1 $R1 - 1
   StrCpy $R2 $R0 1 $R1
   StrCmp $R2 &quot;$\r&quot; loop
   StrCmp $R2 &quot;$\n&quot; loop
   IntOp $R1 $R1 + 1
   IntCmp $R1 0 no_trim_needed
   StrCpy $R0 $R0 $R1
 
 no_trim_needed:
   Pop $R2
   Pop $R1
   Exch $R0
 FunctionEnd
</pre>
<a name="C.3"></a><h2>C.3 Get command line parameters</h2>
<pre> ; GetParameters
 ; input, none
 ; output, top of stack (replaces, with e.g. whatever)
 ; modifies no other variables.
 
 Function GetParameters
 
   Push $R0
   Push $R1
   Push $R2
   Push $R3
   
   StrCpy $R2 1
   StrLen $R3 $CMDLINE
   
   ;Check for quote or space
   StrCpy $R0 $CMDLINE $R2
   StrCmp $R0 '&quot;' 0 +3
     StrCpy $R1 '&quot;'
     Goto loop
   StrCpy $R1 &quot; &quot;
   
   loop:
     IntOp $R2 $R2 + 1
     StrCpy $R0 $CMDLINE 1 $R2
     StrCmp $R0 $R1 get
     StrCmp $R2 $R3 get
     Goto loop
   
   get:
     IntOp $R2 $R2 + 1
     StrCpy $R0 $CMDLINE 1 $R2
     StrCmp $R0 &quot; &quot; get
     StrCpy $R0 $CMDLINE &quot;&quot; $R2
   
   Pop $R3
   Pop $R2
   Pop $R1
   Exch $R0
 
 FunctionEnd
</pre>
<a name="C.4"></a><h2>C.4 Search in a string</h2>
<pre> ; StrStr
 ; input, top of stack = string to search for
 ;        top of stack-1 = string to search in
 ; output, top of stack (replaces with the portion of the string remaining)
 ; modifies no other variables.
 ;
 ; Usage:
 ;   Push &quot;this is a long ass string&quot;
 ;   Push &quot;ass&quot;
 ;   Call StrStr
 ;   Pop $R0
 ;  ($R0 at this point is &quot;ass string&quot;)

 Function StrStr
   Exch $R1 ; st=haystack,old$R1, $R1=needle
   Exch    ; st=old$R1,haystack
   Exch $R2 ; st=old$R1,old$R2, $R2=haystack
   Push $R3
   Push $R4
   Push $R5
   StrLen $R3 $R1
   StrCpy $R4 0
   ; $R1=needle
   ; $R2=haystack
   ; $R3=len(needle)
   ; $R4=cnt
   ; $R5=tmp
   loop:
     StrCpy $R5 $R2 $R3 $R4
     StrCmp $R5 $R1 done
     StrCmp $R5 &quot;&quot; done
     IntOp $R4 $R4 + 1
     Goto loop
 done:
   StrCpy $R1 $R2 &quot;&quot; $R4
   Pop $R5
   Pop $R4
   Pop $R3
   Pop $R2
   Exch $R1
 FunctionEnd
</pre>
<a name="C.5"></a><h2>C.5 Get Windows version</h2>
<pre> ; GetWindowsVersion
 ;
 ; Based on Yazno's function, http://yazno.tripod.com/powerpimpit/
 ; Updated by Joost Verburg
 ;
 ; Returns on top of stack
 ;
 ; Windows Version (95, 98, ME, NT x.x, 2000, XP, 2003)
 ; or
 ; '' (Unknown Windows Version)
 ;
 ; Usage:
 ;   Call GetWindowsVersion
 ;   Pop $R0
 ;   ; at this point $R0 is &quot;NT 4.0&quot; or whatnot
 
 Function GetWindowsVersion
 
   Push $R0
   Push $R1
 
   ClearErrors
 
   ReadRegStr $R0 HKLM \
   &quot;SOFTWARE\Microsoft\Windows NT\CurrentVersion&quot; CurrentVersion

   IfErrors 0 lbl_winnt
   
   ; we are not NT
   ReadRegStr $R0 HKLM \
   &quot;SOFTWARE\Microsoft\Windows\CurrentVersion&quot; VersionNumber
 
   StrCpy $R1 $R0 1
   StrCmp $R1 '4' 0 lbl_error
 
   StrCpy $R1 $R0 3
 
   StrCmp $R1 '4.0' lbl_win32_95
   StrCmp $R1 '4.9' lbl_win32_ME lbl_win32_98
 
   lbl_win32_95:
     StrCpy $R0 '95'
   Goto lbl_done
 
   lbl_win32_98:
     StrCpy $R0 '98'
   Goto lbl_done
 
   lbl_win32_ME:
     StrCpy $R0 'ME'
   Goto lbl_done
 
   lbl_winnt:
 
   StrCpy $R1 $R0 1
 
   StrCmp $R1 '3' lbl_winnt_x
   StrCmp $R1 '4' lbl_winnt_x
 
   StrCpy $R1 $R0 3
 
   StrCmp $R1 '5.0' lbl_winnt_2000
   StrCmp $R1 '5.1' lbl_winnt_XP
   StrCmp $R1 '5.2' lbl_winnt_2003 lbl_error
 
   lbl_winnt_x:
     StrCpy $R0 &quot;NT $R0&quot; 6
   Goto lbl_done
 
   lbl_winnt_2000:
     Strcpy $R0 '2000'
   Goto lbl_done
 
   lbl_winnt_XP:
     Strcpy $R0 'XP'
   Goto lbl_done
 
   lbl_winnt_2003:
     Strcpy $R0 '2003'
   Goto lbl_done
 
   lbl_error:
     Strcpy $R0 ''
   lbl_done:
 
   Pop $R1
   Exch $R0
 
 FunctionEnd
</pre>
<a name="C.6"></a><h2>C.6 Get Internet Explorer version</h2>
<pre> ; GetIEVersion
 ;
 ; Based on Yazno's function, http://yazno.tripod.com/powerpimpit/
 ; Returns on top of stack
 ; 1-6 (Installed IE Version)
 ; or
 ; '' (IE is not installed)
 ;
 ; Usage:
 ;   Call GetIEVersion
 ;   Pop $R0
 ;   ; at this point $R0 is &quot;5&quot; or whatnot

 Function GetIEVersion
 Push $R0
   ClearErrors
   ReadRegStr $R0 HKLM &quot;Software\Microsoft\Internet Explorer&quot; &quot;Version&quot;
   IfErrors lbl_123 lbl_456

   lbl_456: ; ie 4+
     Strcpy $R0 $R0 1
   Goto lbl_done

   lbl_123: ; older ie version
     ClearErrors
     ReadRegStr $R0 HKLM &quot;Software\Microsoft\Internet Explorer&quot; &quot;IVer&quot;
     IfErrors lbl_error

       StrCpy $R0 $R0 3
       StrCmp $R0 '100' lbl_ie1
       StrCmp $R0 '101' lbl_ie2
       StrCmp $R0 '102' lbl_ie2

       StrCpy $R0 '3' ; default to ie3 if not 100, 101, or 102.
       Goto lbl_done
         lbl_ie1:
           StrCpy $R0 '1'
         Goto lbl_done
         lbl_ie2:
           StrCpy $R0 '2'
         Goto lbl_done
     lbl_error:
       StrCpy $R0 ''
   lbl_done:
   Exch $R0
 FunctionEnd
</pre>
<a name="C.7"></a><h2>C.7 Is .NET Framework installed?</h2>
<pre> ; IsDotNETInstalled
 ;
 ; Usage:
 ;   Call IsDotNETInstalled
 ;   Pop $0
 ;   StrCmp $0 1 found.NETFramework no.NETFramework

 Function IsDotNETInstalled
   Push $0
   Push $1
   Push $2
   Push $3
   Push $4

   ReadRegStr $4 HKEY_LOCAL_MACHINE \
     &quot;Software\Microsoft\.NETFramework&quot; &quot;InstallRoot&quot;
   # remove trailing back slash
   Push $4
   Exch $EXEDIR
   Exch $EXEDIR
   Pop $4
   # if the root directory doesn't exist .NET is not installed
   IfFileExists $4 0 noDotNET

   StrCpy $0 0

   EnumStart:

     EnumRegKey $2 HKEY_LOCAL_MACHINE \
       &quot;Software\Microsoft\.NETFramework\Policy&quot;  $0
     IntOp $0 $0 + 1
     StrCmp $2 &quot;&quot; noDotNET

     StrCpy $1 0

     EnumPolicy:

       EnumRegValue $3 HKEY_LOCAL_MACHINE \
         &quot;Software\Microsoft\.NETFramework\Policy\$2&quot; $1
       IntOp $1 $1 + 1
        StrCmp $3 &quot;&quot; EnumStart
         IfFileExists &quot;$4\$2.$3&quot; foundDotNET EnumPolicy

   noDotNET:
     StrCpy $0 0
     Goto done

   foundDotNET:
     StrCpy $0 1

   done:
     Pop $4
     Pop $3
     Pop $2
     Pop $1
     Exch $0
 FunctionEnd
</pre>
<a name="C.8"></a><h2>C.8 Is Macromedia Flash Player installed?</h2>
<pre> ; IsFlashInstalled
 ;
 ; By Yazno, http://yazno.tripod.com/powerpimpit/
 ; Returns on top of stack
 ; 0 (Flash is not installed)
 ; or
 ; 1 (Flash is installed)
 ;
 ; Usage:
 ;   Call IsFlashInstalled
 ;   Pop $R0
 ;   ; $R0 at this point is &quot;1&quot; or &quot;0&quot;

 Function IsFlashInstalled
  Push $R0
  ClearErrors
  ReadRegStr $R0 HKCR &quot;CLSID\{D27CDB6E-AE6D-11cf-96B8-444553540000}&quot; &quot;&quot;
  IfErrors lbl_na
    StrCpy $R0 1
  Goto lbl_end
  lbl_na:
    StrCpy $R0 0
  lbl_end:
  Exch $R0
 FunctionEnd
</pre>
<a name="C.9"></a><h2>C.9 Connect to the Internet</h2>
<pre> ; ConnectInternet (uses Dialer plugin)
 ; Written by Joost Verburg 
 ;
 ; This function attempts to make a connection to the internet if there is no
 ; connection available. If you are not sure that a system using the installer
 ; has an active internet connection, call this function before downloading
 ; files with NSISdl.
 ; 
 ; The function requires Internet Explorer 3, but asks to connect manually if
 ; IE3 is not installed.
 
 Function ConnectInternet
 
   Push $R0
     
     ClearErrors
     Dialer::AttemptConnect
     IfErrors noie3
     
     Pop $R0
     StrCmp $R0 &quot;online&quot; connected
       MessageBox MB_OK|MB_ICONSTOP &quot;Cannot connect to the internet.&quot;
       Quit ;This will quit the installer. You might want to add your own error handling.
     
     noie3:
   
     ; IE3 not installed
     MessageBox MB_OK|MB_ICONINFORMATION &quot;Please connect to the internet now.&quot;
     
     connected:
   
   Pop $R0
   
 FunctionEnd
</pre>
<a name="C.10"></a><h2>C.10 Get Installer Filename</h2>
<pre> System::Call 'kernel32::GetModuleFileNameA(i 0, t .R0, i 1024) i r1'
 ;$R0 will contain the installer filename
</pre>
<a name="C.11"></a><h2>C.11 Prevent Multiple Instances</h2>

<p>Put the following code in your <a href="Chapter4.html#4.7.2.1.2">.onInit function</a>:</p>
<pre> System::Call 'kernel32::CreateMutexA(i 0, i 0, t &quot;myMutex&quot;) i .r1 ?e'
 Pop $R0
 
 StrCmp $R0 0 +3
   MessageBox MB_OK|MB_ICONEXCLAMATION &quot;The installer is already running.&quot;
   Abort
</pre>

<p>'myMutex' should be replaced by a unique value.</p>
<a name="C.12"></a><h2>C.12 More</h2>

<p>You can find more useful scripts at <a href="http://nsis.sourceforge.net/archive/">the NSIS Archive</a>, <a href="http://forums.winamp.com/forumdisplay.php?s=&forumid=65">the NSIS forum</a> and <a href="http://nsis.sourceforge.net/">NSIS development page</a>.</p>
<p><a href='AppendixB.html'>Previous</a> | <a href='Contents.html'>Contents</a> | <a href='AppendixD.html'>Next</a></p>

<hr />

<address>
</address>
</body>

</html>
